10 HOME
15 HGR2
16 HGR
17 POKE 49234,0:REM "FULL GRAPHICS MODE" 
18 S=0
19 Y=0
21 DIM YP(3):YP(1)=0:YP(2)=0:YP(3)=20:REM "PREV PG1,PG2 AND CURRENT COORDS FOR PLAYER"
22 DIM YE(3):YE(1)=0:YE(2)=0:YE(3)=20:REM "PREV PG1,PG2 AND CURRENT COORDS FOR ENEMY"
23 DIM YB(3):YB(1)=0:YB(2)=0:YB(3)=20:REM "PREV PG1,PG2 AND CURRENT COORDS FOR BALL"
24 DIM XB(3):XB(1)=0:XB(2)=0:XB(3)=0:REM "PREV PG1,PG2 AND CURRENT COORDS FOR BALL"
25 SB=30:SP=5:SI=SB:REM "SIZE OF BAT,PUCK,CURRENT ITEM"
28 XP=20:X=0:REM "X LOCATION OF PLAYER, GENERAL X"
29 VX=5:VY=5:REM "X AND Y VELOCITY OF BALL"
498 GOTO 1000
500 FOR I=0 TO 5:HPLOT X+I,Y TO X+I,Y+SI:NEXT:RETURN:REM "DRAW 5PX WIDTH RECTANGLE"
899 REM "INIT"
999 REM "FLIP SCREEN"
1000 POKE 49236+S,0:POKE 230,32+((1-S)*32):S=1-S
1100 SI=SB:HCOLOR=0:X=XP:Y=YP(S+1):GOSUB 500:Y=YP(3):YP(S+1)=Y:HCOLOR=2:GOSUB 500
1101 SI=SP:HCOLOR=0:X=XB(S+1):Y=YB(S+1):GOSUB 500:Y=YB(3):YB(S+1)=Y:X=XB(3):XB(S+1)=X:HCOLOR=1:GOSUB 500
1110 POKE 49236,0
1120 POKE 49237,0
1500 K1=PEEK(49152)
1509 REM "UPDATE LOCATION OF PLAYER BAT"
1510 IF K1=215 THEN YP(3)=YP(3)-4:POKE 49168,0:IF YP(3) < 0 THEN YP(3)=0:REM "215 IS KEY_W"
1520 IF K1=211 THEN YP(3)=YP(3)+4:POKE 49168,0:IF YP(3)+SB > 191 THEN YP(3)=191-SB:REM "211 IS KEY_S"
1599 REM "UPDATE LOCATION OF BALL"
1600 IF YB(3) + SP + VY > 191 AND VY > 0 THEN VY=0-VY
1601 IF YB(3) + VY < 0 AND VY < 0 THEN VY=0-VY
1602 IF XB(3) + SP + VX > 279 AND VX > 0 THEN VX=0-VX
1603 IF XB(3) + VX < 0 AND VX < 0 THEN VX=0-VX
1604 FL=0:REM "INIT FLIP TO FALSE"
1605 IF YB(3) + VY >= YP(3) AND YB(3) + VY < YP(3) + SB AND XB(3) + VX >= XP AND XB(3) + VX < XP + 5 THEN FL=1:REM "UPPER LEFT CORNER HITS PLAYER"
1606 IF YB(3) + VY >= YP(3) AND YB(3) + VY < YP(3) + SB AND XB(3) + 5 + VX >= XP AND XB(3) + 5 + VX < XP + 5 THEN FL=1:REM "UPPER RIGHT CORNER HITS PLAYER"
1607 IF YB(3) + VY + SP >= YP(3) AND YB(3) + VY + SP < YP(3) + SB AND XB(3)+ VX >= XP AND XB(3) + VX < XP + 5 THEN FL=1:REM "BOTTOM LEFT CORNER HITS PLAYER"
1608 IF YB(3)  + VY + SP >= YP(3) AND YB(3) + VY + SP < YP(3) + SB AND XB(3) + 5 + VX >= XP AND XB(3) + 5 + VX < XP + 5 THEN FL=1:REM "BOTTOM RIGHT CORNER HITS PLAYER"
1615 IF FL=1 THEN VX=0-VX
1620 XB(3)=XB(3)+VX:YB(3)=YB(3) + VY
9999 GOTO 1000
10000 END